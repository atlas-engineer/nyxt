#+TITLE: Nyxt Extension Expos√©
This Document is a place for users of Nyxt to share useful
customizations as a community. If you have something you wish to add,
please feel free to open a PR with your customization.

* Contents                                                              :TOC:
- [[#setting-the-start-page][Setting the start page]]
- [[#creating-your-own-parenscript][Creating your own Parenscript]]
- [[#running-shell-commands-within-next][Running shell commands within Nyxt]]
- [[#reloading-the-page][Reloading the page]]
- [[#scroll-the-page-up-and-down-by-a-screen][Scroll the page up and down by a screen]]
- [[#send-current-url-to-kdeconnect-devices][Send current URL to KDEConnect devices]]

* Creating your own Parenscript
You can call any parenscript within the browser through the convient
~define-parenscript~ macro. A really simple example below
shows how you could set the body with the URL of the current page.

#+NAME: hell-url
#+BEGIN_SRC lisp
(define-parenscript hello-url ()
  (defun set-body (text)
    (setf (ps:chain document body inner-text) text)
    ;; return nil to avoid json decode errors
    nil)
  (set-body (ps:chain document -u-r-l)))
#+END_SRC

A ~define-parenscript~ without arguments will automatically generate a
~defparameter~ named ~hello-url~ with the actual Javascript, and a function
~(hello-url)~ that you can invoke to act upon the currently active buffer.

* Running shell commands within Nyxt
Shell mode is an extension for running simple one liner shell commands
and seeing the output. There is no history or variables. Use ~C-x s~
to enter a ~*shell*~ buffer, ~c~ to enter a command and ~k~ to clear
the command output.

#+NAME: shell-mode
#+BEGIN_SRC lisp
(define-mode shell-mode ()
    "A basic shell prompt."
    ((keymap-schemes
      :initform
      (let ((map (make-keymap)))
        (define-key :keymap map
          "c" 'run-shell-command
          "k" 'clear-shell)
        (list :emacs map
              :vi-normal map)))))

(define-parenscript clear-shell-output ()
    (setf (ps:chain document body inner-h-t-m-l) ""))

(define-command clear-shell (shell-mode)
  "Clear the output in the shell buffer."
  (ipc-buffer-evaluate-javascript
   (active-buffer *browser*)
   (clear-shell-output)))

(define-parenscript append-output (output)
  (setf (ps:chain document body inner-h-t-m-l)
        (ps:chain document body inner-h-t-m-l
                  (concat (ps:lisp
                           (format nil "<pre><code>~a</code></pre><br/>" output))))))

(define-command run-shell-command (shell-mode)
  "Run a shell command."
  (with-result
      (input (read-from-minibuffer
              (minibuffer *browser*)
              :input-prompt "Run in shell:"))
    (ipc-buffer-evaluate-javascript
     (active-buffer *browser*)
     (append-output
      :output
      (uiop:run-program input :force-shell t :output :string)))))

(define-command shell ()
  "Open a shell buffer."
  (set-active-buffer *browser* (make-buffer :name "*shell*" :default-modes '(shell-mode))))

(define-key :keymap *my-keymap* "C-x s" #'shell)
#+END_SRC

* Reloading the page
The following function provides a way to reload the current page.

#+NAME: reload-page
#+BEGIN_SRC lisp
;; Reload page with C-r
(define-parenscript %reload-page ()
  (ps:chain location (reload)))

(define-command reload-page ()
  "Reload page."
  (%reload-page))

(define-key :keymap *my-keymap* "C-r" #'reload-page)
#+END_SRC

* Scroll the page up and down by a screen
You can use the following functions to scroll the page up or down by
one screen.

#+NAME: scroll-page-up-and-down
#+BEGIN_SRC lisp
(define-parenscript %scroll-page-down ()
  (ps:chain window (scroll-by 0 (ps:@ window inner-height))))

(define-parenscript %scroll-page-up ()
  (ps:chain window (scroll-by 0 (- (ps:@ window inner-height)))))

(define-command scroll-page-down ()
  "Scroll down by one page height."
  (%scroll-page-down))

(define-command scroll-page-up ()
  "Scroll up by one page height."
  (%scroll-page-up))

;; Bind the new functions.
(define-key :keymap *my-keymap* "C-v" #'scroll-page-down)
(define-key :keymap *my-keymap* "M-v" #'scroll-page-up)
#+END_SRC

* Send current URL to KDEConnect devices
  The following snippets call kdeconnect command to send a URL to the devices selected via minibuffer. Change the kdeconnect-command string to whatever the kdeconnect command is called on your system.

  It uses =match-bind= and =match-split= from =cl-irregsexp= package to parse the device id, which is available via quicklisp:

  #+begin_src lisp
    (ql:quickload "cl-irregsexp")
  #+end_src

  The =butlast= in the following procedure removes the device count string from the device list.
  #+begin_src lisp
    (defun kdeconnect-devices (&optional (kdeconnect-command "kdeconnect-cli")
                                 &rest args)
      "List KDEConnect devices."
      (butlast (cl-irregsexp:match-split #\Linefeed (apply #'nyxt::run-program-to-string
                                                           kdeconnect-command args))))
  #+end_src

    The following command lists available devices with =-a= option. If none are already detected, it probes them with =-l= option (instead of =--refresh=) and lists available devices again. After parsing the device id from selected string, it executes the commands to send the url to selected devices.

  #+begin_src lisp
    (define-command kdeconnect-send-url (&optional (url (url (current-buffer)))
                                         (kdeconnect-command "kdeconnect-cli"))
      "Sends a URL to a device via kdeconnect cli interface."
      (let ((device-strings (trivia:match (kdeconnect-devices kdeconnect-command "-a")
                              ((guard device (not (null device))) device)
                              (progn (kdeconnect-devices kdeconnect-command "-l")
                                     (kdeconnect-devices kdeconnect-command "-a")))))
        (with-result (devices
                      (read-from-minibuffer
                       (make-instance 'minibuffer
                                      :input-prompt "Select Devices: "
                                      :multi-selection-p t
                                      :completion-function (lambda (input)
                                                             (fuzzy-match input device-strings)))))
          (mapcar (lambda (dev)
                    (cl-irregsexp:match-bind ("- " name ": " id " (" pairing-status ")") dev
                                             (nyxt::run-program-to-string kdeconnect-command
                                                                          "--share" url
                                                                          "-d" id)))
                  devices))))
  #+end_src
